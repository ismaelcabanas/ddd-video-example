group 'cabanas.garcia.ismael.tdd'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.4.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: "org.springframework.boot", name: "spring-boot-gradle-plugin", version: springBootVersion
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    integrationtestCompile.extendsFrom testCompile
    integrationtestRuntime.extendsFrom testRuntime
    acceptancetestCompile.extendsFrom testCompile
    acceptancetestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationtest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            // Use "java" if you don't use Scala as a programming language
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
    acceptancetest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            // Use "java" if you don't use Scala as a programming language
            srcDir file('src/acceptance-test/java')
        }
        resources.srcDir file('src/acceptance-test/resources')
    }
}

dependencies {
    // Spring dependencies
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile("org.apache.commons:commons-lang3:3.5")

    testCompile("org.assertj:assertj-core:3.6.1")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    acceptancetestCompile("io.rest-assured:rest-assured:3.0.6")
    acceptancetestCompile("io.rest-assured:json-path:3.0.6")
}
